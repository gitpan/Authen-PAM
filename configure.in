dnl -*-m4-*-

AC_INIT

AC_PROG_CC
AC_PROG_CPP

dnl Add -ldl to LIBS if necessary for linking the other test scripts
AC_CHECK_LIB(dl, dlopen)

dnl This variable is only used in the tests and is not passed to the Makefile
LIBS="-lpam"

dnl Checks if the pam library supports the environment handling functions
AC_CHECK_FUNCS(pam_getenv)

dnl Checks if pam_strerror() takes a pam_handle
AC_CACHE_CHECK(if pam_strerror takes a pam_handle_t argument, 
ac_cv_pam_strerror_needs_pamh,
AC_TRY_COMPILE([#include <security/pam_appl.h>],
[pam_handle_t *pamh;
pam_strerror(pamh, PAM_SUCCESS)],
[AC_DEFINE_UNQUOTED(PAM_STRERROR_NEEDS_PAMH)
ac_cv_pam_strerror_needs_pamh=yes],
ac_cv_pam_strerror_needs_pamh=no))

dnl Checks for various PAM constants
AC_DEFUN(AC_CHECK_PAM_CONST,
[AC_CACHE_CHECK(for $1, ac_cv_have_$1,
AC_TRY_COMPILE([#include <security/pam_appl.h>],
[int i = $1], 
[AC_DEFINE_UNQUOTED(HAVE_$1) 
ac_cv_have_$1=yes], ac_cv_have_$1=no))])

AC_CHECK_PAM_CONST(PAM_AUTHTOKEN_REQD)
AC_CHECK_PAM_CONST(PAM_AUTHTOK_EXPIRED)
AC_CHECK_PAM_CONST(PAM_AUTHTOK_RECOVER)
AC_CHECK_PAM_CONST(PAM_AUTHTOK_RECOVERY)
AC_CHECK_PAM_CONST(PAM_BAD_ITEM)
AC_CHECK_PAM_CONST(PAM_CONV_AGAIN)
AC_CHECK_PAM_CONST(PAM_CRED_DELETE)
AC_CHECK_PAM_CONST(PAM_CRED_ESTABLISH)
AC_CHECK_PAM_CONST(PAM_CRED_REFRESH)
AC_CHECK_PAM_CONST(PAM_CRED_REINITIALIZE)
AC_CHECK_PAM_CONST(PAM_DELETE_CRED)
AC_CHECK_PAM_CONST(PAM_ESTABLISH_CRED)
AC_CHECK_PAM_CONST(PAM_INCOMPLETE)
AC_CHECK_PAM_CONST(PAM_MODULE_UNKNOWN)
AC_CHECK_PAM_CONST(PAM_NEW_AUTHTOK_REQD)
AC_CHECK_PAM_CONST(PAM_RADIO_TYPE)
AC_CHECK_PAM_CONST(PAM_REFRESH_CRED)
AC_CHECK_PAM_CONST(PAM_REINITIALIZE_CRED)

dnl Checks if the dl library supports the RTLD_GLOBAL flag
AC_CACHE_CHECK(for RTLD_GLOBAL flag, pam_cv_decl_rtld_global,
AC_EGREP_CPP(have_rtld_global,
[#include <dlfcn.h>
#ifdef RTLD_GLOBAL
have_rtld_global
#endif],
pam_cv_decl_rtld_global=yes, pam_cv_decl_rtld_global=no))
if test $pam_cv_decl_rtld_global = yes; then
    DL_LOAD_FLAGS='sub dl_load_flags { 0x01 }'
else
    DL_LOAD_FLAGS=''
fi
AC_SUBST(DL_LOAD_FLAGS)


AC_OUTPUT(pam.cfg PAM.pm)
